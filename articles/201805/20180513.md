# レイトレーシング(12): 鏡面反射と屈折

## 0. 以前の記事の訂正

  第8回の記事で完全拡散反射の場合のBRDF($f_r$)を下記のように示した。

  \[
  f_r = \frac{1}{2\pi}
  \]

  が、間違いだった。shocker-0x15 さんからご指摘をいただきました。
  ありがとうございます。正しい式は以下。

  \[
  f_r = \frac{1}{\pi}
  \]

  これにより、得られる輝度が全体的にほぼ倍になるが、輝度を画面の
  色に変換するところで調整してやればいいので大きな問題はなかった。
  やれやれ。

## 1. 今回の改善

  今回は例トレーシング回の最終回ということで、やっと目標だった集光
  模様の実現に取り組むことにする。また、ついでに球以外の形状の
  サポート、設定ファイルの対応もやってみた。

### -1. Material型

まず最初に`Material`型の拡張について説明したい。`Material`型は
物体の物性や模様を表すために定義した型だ。前回までは単純な
完全拡散面を想定していたためパラメータは少ししかなかった（コード
自体には書いていても使っていないものもあった）。鏡面反射・屈折を
サポートするにあたりいくつかのパラメータを追加したし、実際に
値を使うようにコードを拡張した。下表に従来と今回の対応を示す。

|parameter    |型    |説明|前回版|今回|
|:-:|:-:|:-:|:-:|:-:|
|emittance    |Radiance|自己発光強度|○|○|
|reflectance  |Color   |拡散反射率(≒物体色?)|○|○|
|specularRefl |Color   |鏡面反射率(金属反射)|-|○|
|transmittance|Color   |透過率|-|-|
|ior          |Color   |屈折率|-|○|
|diffuseness  |Double  |拡散度※1|○|○|
|metalness    |Double  |金属性※2|-|○|
|smoothness   |Double  |平滑度|-|-|

> ※1: 拡散反射と鏡面反射のあり合いを表す。1.0だと完全拡散反射、0.0
  だと完全鏡面反射となる。鏡面反射方向からの光をどれだけ反射するか。

> ※2: 金属性と書いたが金属反射と透過の割合を表す。1.0だと完全に
  不透明で金属反射のみ、0.0だとガラスのような透明物体となる。

今回もまだ対応できていない`transmittance`は透明物質内での光の通過
割合を指定することで、色ガラスのような物体を表すため、smoothnessは
表面のざらつき具合でぼやけたハイライトなどを表すためのパラメータだ。
ぜひともこれらの実装をしたいが、今後の課題とする。

`ior`の型が`Color`になっているのは、色（光の波長）毎に屈折率が違う
場合があるため。三角プリズムによる光の分散を再現するためだ。とはいえ、
現状は赤、緑、青の3波長しか扱っていないため、いわゆる虹色にならず
紫部分は表現できない紫を加えた4波長とかにすればそれも可能になろうが
光の三原色じゃなくなるので画面上の色へどう変換するか考えないと
いけない。

各パラメータがどのように使われるかは後の節で説明する。

### -2. 鏡面反射・屈折ベクトル

反射・屈折ベクトルの求め方については、いろいろなところに情報があるので
ここでは割愛する。数式とコードのみ示すことにしよう。各記号・文字がわかり
易いように、全体を図にするとこんな感じ。

（図）

#### (反射)

入射ベクトル $\bm{e}$ 、法線ベクトル $\bm{n}$ とする時、求める反射ベクトル
$\bm{v_r}$ は、

\[
\bm{v_r} = \bm{e} - 2 \langle \bm{e}, \bm{n} \rangle \bm{n}
\]

である。ただし、$\bm{e}, \bm{n}$ は正規化(=長さが1)されているものとする。
コードは次の通り。

```haskell :Geometry.hs
specularReflection :: Direction3 -> Direction3 -> (Direction3, Double)
specularReflection n e
  | v == Nothing = (n, 0.0)
  | c < 0.0      = (fromJust v, -c)
  | otherwise    = (fromJust v,  c)
  where
    c = e <.> n
    v = normalize (e - (2.0 * c) *> n)
```

まあ、コードは式をそのまま表現しただけ、である。戻り値は反射ベクトル
だけでなく内積 $\langle \bm{e}, \bm{n} \rangle$ すなわち $\cos \theta$
( $\theta$ は $\bm{e}$ と $\bm{n}$ のなす角) も含めている。あとで使われる
ことが多いので。

#### (屈折)

絶対屈折率 $\eta_1$ の物質から絶対屈折率 $\eta_2$ の物質に光が入射した
場合、入射角 $\theta$ に対し、屈折ベクトルの角度が法線に対して $\phi$ と
なる場合、それらの間には以下の関係が成り立つという(理屈はよくわかっていない)。

\[
\eta_1 \sin \theta = \eta_2 \sin \phi
\]

この関係式を使うと、（途中の式変形などはよくわからないが）屈折ベクトル
 $\bm{v_t}$ は次の式で求められる。

\[
\bm{v_t} = \frac{\eta_1}{\eta_2} \left( \bm{e} + \left( \cos \theta - \sqrt{\left( \frac{\eta_2}{\eta_1} \right)^2 + \cos^2 \theta - 1} \right) \bm{n} \right)
\]

ここで $\cos \theta$ は反射と同じく内積 $\langle \bm{e}, \bm{n} \rangle$ である。
コードは下記のとおり。

```haskell :Geometry.hs
specularRefraction :: Double -> Double -> Double -> Direction3 -> Direction3
                   -> (Direction3, Double)
specularRefraction ior0 ior1 c0 ed n
  | t == Nothing = (o3, 0.0)
  | otherwise    = (fromJust t, ior')
  where
    n' = if ed <.> n > 0.0 then negate n else n
    ior' = ior0 / ior1
    a = c0 - sqrt (1.0 / (ior' * ior') + c0 * c0 - 1.0)
    t = normalize (ior' *> (ed + a *> n'))
```

引数の`ior0`、`ior1`、`c0`は数字が紛らわしいがそれぞれ $\eta_1$, $\eta_2$,
 $\cos \theta$を意味する。`ed`は入射ベクトル $\bm{e}$ である。`where`節の下
から少し説明する。屈折ベクトルを求めるには入射光が通ってきたのと同じ物質の
方へ向いた法線ベクトルが必要だが、本プログラムでは交点での法線は常に物体の
定義により一方向のみである。つまり球ならその外側に向いた法線ベクトルとなる。
しかし屈折では物体内部を光が通ることもあり、その内部を通った光が物体表面で
反射屈折する時に必要なのは物体の内側に向いた法線ベクトルである。そのため
わざわざ $\cos \theta$ の符号をみて $\bm{n}$ を反転させている(`n'`)。
このことは屈折だけでなく反射も同じだが、ガードによる条件分岐で済ましている。
あとは上述の数式通りなので特に問題ないだろう。屈折はおまけとして比屈折率
($\frac{\eta_1}{\eta_2}$)も戻り値に入れている。

#### (Schlickの近似式)

鏡面反射・屈折では、物体表面の反射率が入射角により変化する。本来は
Fresnelの公式というのがあって、これがなかなかややこしい。多分計算
負荷も高い。そこで便利な"Schlickの近似式"というのを代わりに使うそうだ。

\[
F_r \approx F_0 + (1 - F_0) (1 - \cos \theta)^5
\]

ここで、$F_0$ は物質の法線方向からの光の反射率である。物質によりその
値が異なる。上記近似式とともに、下記のWebページにいくつかサンプルがある。

https://ja.wikipedia.org/wiki/フレネルの式
http://d.hatena.ne.jp/hanecci/20130525/p3
https://yokotakenji.me/log/math/4501/

ここで、上述の鏡面反射ベクトル計算で返される $\cos \theta$ が必要なのだ。
コードはこちら。$F_0$ (`Color`型)と $\cos \theta$ が引数である。
あとは上記の近似式そのままだ。

```haskell :Physics.hs
reflectionIndex :: Color -> Double -> Color
reflectionIndex (Color r g b) c =
  Color (r + (1-r) * c') (g + (1-g) * c') (b + (1-b) * c')
  where
    c' = (1.0 - c) ** 5.0
```

### -3. フォトン追跡

鏡面反射・屈折が追加されたので、フォトン追跡もそれに合わせて
いろいろ拡張せねばなるまい。これまでは拡散面だけだったので、
その反射率によって拡散反射か吸収（追跡終了）しかなかったが、
今回は物性によりそのバリエーションが増えた。

* 拡散反射か鏡面反射か吸収か
* 反射か屈折か

この辺の場合分けを前述の`Material`の各パラメータとロシアンルーレットを
使って表現していくのだ。解り易いようにフローチャートで表そう。
下図に前回（拡散反射面だけ）と今回（鏡面反射・屈折追加）のフローを示そう。
前回のフローは今回のフローの一部になっている(赤点線の部分)のだ。

（フロー図）

次にコードで比べてみる。前回のコードはこちら。

```haskell
tracePhoton :: [Object] -> Int -> Photon -> IO [PhotonCache]
tracePhoton os l (wl, r) = do
  let is = calcIntersection r os
  if is == Nothing
    then return []
    else do
      let (p, n, m) = fromJust is
      i <- russianRoulette wl [reflectance m]
      pcs <- if i > 0
        then reflect p n os l wl
        else return []
      if (useClassicForDirect == False || l > 0) && diffuseness m > 0.0
        then return $ ((wl, initRay p (getDir r)) : pcs)
        else return pcs
```

中程の`russianRoulette`にて拡散反射率による分岐をしている。関数`reflect`は
拡散反射してさらにフォトンを追跡するものだ。最後の`if`は前回取り入れた
画質向上策である。

一方、今回拡張したコードはこちら。

```haskell
tracePhoton :: Bool -> Material -> [Object] -> Int -> Photon
            -> IO [PhotonCache]
tracePhoton _   _   _   10 _        = return []
tracePhoton !uc !m0 !os !l !(wl, r)
  | is == Nothing = return []
  | otherwise     = do
    let
      is' = fromJust is
      (p, _, m) = is'
      d = diffuseness m
    i <- russianRoulette [d]
    ref <- if i > 0
      then reflectDiff uc m0 os l wl is'
      else reflectSpec uc m0 os l (wl, r) is'
    if (uc == False || l > 0) && d > 0.0
      then return $ ((wl, initRay p $ getDir r) : ref)
      else return ref
  where
    is = calcIntersection r os

reflectDiff :: Bool -> Material -> [Object] -> Int -> Wavelength
            -> Intersection -> IO [PhotonCache]
reflectDiff uc m0 os l wl (p, n, m) = do
  i <- russianRoulette [selectWavelength wl $ reflectance m]
  if i > 0
    then do  -- diffuse reflection
      dr <- diffuseReflection n
      tracePhoton uc m0 os (l+1) $ (wl, initRay p dr)
    else return [] -- absorption

reflectSpec :: Bool -> Material -> [Object] -> Int -> Photon -> Intersection
            -> IO [PhotonCache]
reflectSpec uc m0 os l (wl, (_, ed)) (p, n, m) = do
  let
    f0 = selectWavelength wl $ specularRefl m
    (rdir, cos0) = specularReflection n ed
    f' = f0 + (1.0 - f0) * (1.0 - cos0) ** 5.0
  j <- russianRoulette [f']
  if j > 0
    then tracePhoton uc m0 os (l+1) (wl, initRay p rdir)
    else do
      if (selectWavelength wl $ ior m) == 0.0
        then return []   -- non transparency
        else reflectTrans uc m0 os l wl ed (p, n, m) cos0

reflectTrans :: Bool -> Material -> [Object] -> Int -> Wavelength -> Direction3
             -> Intersection -> Double -> IO [PhotonCache]
reflectTrans uc m0 os l wl ed (p, n, m) c0 = do
  let
    ior0 = selectWavelength wl $ ior m0
    ior1 = selectWavelength wl $ ior m
    (tdir, ior') = specularRefraction ior0 ior1 c0 ed n
    m0' = if tdir <.> n < 0.0 then m else m_air
  tracePhoton uc m0' os (l+1) (wl, initRay p tdir)
```

一見して以前と比べてかなり複雑になったのがわかるだろう。`russianRoulette`が
いくつか使われており、それが各分岐になっている。`reflectDiff`, `reflectSpec`,
`reflectTrans`はそれぞれ拡散反射、鏡面反射、屈折の場合のフォトン追跡の処理だ。

屈折をサポートしたので、最初の目的だった「集光模様」が得られる
はずだ！というわけで、ガラス球を配置してフォトンマップを生成してみよう。

（フォトンマップ）

左が不透明な拡散反射面の球の場合、右がガラス球だ。左は拡散反射するので
球の形にフォトンが記録されていて下に球の影が見える。一方右はガラス球
なので球面状にフォトンは記録されず、代わりに球の影の真ん中にフォトンが
集中している部分がある。これが集光模様になるのだ！

### -4. レンダリング方程式(?)

さて、いよいよレンダリングの拡張だ。フォトンマップをみる限り、想定したとおり
集光模様が描けそうな気がする！

レンダリングは、注目する点（視線レイと物体の交点）にいろいろな方向から届く
光を集計して輝度として返せば良い。ただ、言葉では簡単だがこれがなかなか難しい。
私はいまだに良い（正確な/物理的に正しい/表現力の高い/…）集計方法がわからない。
とはいえわからないなりに作るしかないので、今回採用した集計方法を説明しよう。

まずは前回までの式。

\[
L_o = L_e + d \cdot \frac{1}{\pi} (L_l + L_d)
\]

各文字はそれぞれ以下を表す。

* $L_o$ : 交点から視線方向への輝度
* $L_e$ : 物体の発光（輝度）
* $L_l$ : 光源からの直接光（フォトンマップから推定することも可）
* $L_d$ : 間接光（フォトンマップから推定）
* $d$ : 拡散度（`Material`中の`diffuseness`）

これだとそんなに複雑ではない。


  式の新旧、コード

  例：銀＝鏡、ガラス（集光模様）

## 2. 作例

  物質の変化（石膏、サンゴ、金、プリズムガラス）
  直方体、正四面体、球2個

## 3. まとめ



※ 今回分のソースは[こちら](https://github.com/eijian/raytracer/tree/version-2.5.2.0)。
